buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

allprojects {
    repositories {
        maven { url "http://dl.bintray.com/qaware-oss/maven" }
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'license'

    version = '1.0.0'
    group = 'de.qaware.showcase.spock'

    // Spock works with Java 1.5 and above
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        // mandatory dependencies for using Spock
        compile 'org.codehaus.groovy:groovy-all:2.4.1'
        testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-1'
        testCompile 'junit:junit:4.12'

        // optional dependencies for using Spock
        // allows mocking of classes (in addition to interfaces)
        testRuntime 'cglib:cglib-nodep:3.1'

        // allows mocking of classes without default constructor (together with CGLIB)
        testRuntime 'org.objenesis:objenesis:2.1'

        testCompile 'com.athaydes:spock-reports:1.2.5'
    }

    license {
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
        header file("$rootProject.projectDir/LICENSE")
        skipExistingHeaders true
        include '**/*.java'
        include '**/*.groovy'
        exclude '**/package-info.java'
    }

    test {
        reports {
            junitXml.enabled = false
            html.enabled = true
        }
    }
}
